apply plugin: 'idea'
apply plugin: 'java'

repositories {
    mavenCentral()
}

jar {
    baseName = 'webcrawler-ui'
    version = '0.0.1-SNAPSHOT'
}

ext {
    isWindows = org.gradle.internal.os.OperatingSystem.current().windows
}

task cleanNodeModules(type: Delete) {
    delete 'src/node_modules'
}

clean.dependsOn cleanNodeModules

task npmInstall(type: Exec) {
    workingDir 'src'

    if (isWindows) {
        commandLine 'cmd', '/c', 'npm', 'install'
    } else {
        commandLine 'npm', 'install'
    }
}

task typescriptCompile(type: Exec, dependsOn: 'npmInstall') {
    workingDir 'src'

    if (isWindows) {
        commandLine 'cmd', '/c', 'npm', 'run-script', 'tsc'
    } else {
        commandLine 'npm', 'run-script', 'tsc'
    }
}

task copyResources(type: Copy, dependsOn: 'typescriptCompile') {
    from('src') {
        exclude '**/*.ts', 'node_modules'
    }
    into 'build/resources/main/static'
    includeEmptyDirs = false
}

task npmInstallProd(type: Exec, dependsOn: 'copyResources') {
    workingDir 'build/resources/main/static'

    if (isWindows) {
        commandLine 'cmd', '/c', 'npm', 'install', '--production'
    } else {
        commandLine 'npm', 'install', '--production'
    }
}

build.dependsOn npmInstallProd

task devServer(type: Exec) {
    workingDir 'src'

    if (isWindows) {
        commandLine 'cmd', '/c', 'npm', 'start'
    } else {
        commandLine 'npm', 'start'
    }
}

idea {
    module {
        sourceDirs += file('src')

        excludeDirs += file('src/node_modules')
    }
}